[script]
n0=;------------- $parse_url -------------
n1=;
n2=; Parse a URL and return its components
n3=; Inspired by php function parse_url()
n4=;
n5=; Usage: $parse_url(URL).prop
n6=; ------------------------------------
n7=; Properties List:
n8=; scheme, host, user, pass, path, query, fragment
n9=;
n10=; Example:
n11=; $parse_url(http://username:mypasswd@some.site.com/some/subdir/a_file.html?querystring=1#anchor)
n12=; 
n13=; Scheme   => http
n14=; Host     => some.site.com
n15=; User     => username
n16=; Pass     => mypasswd
n17=; Path     => /some/subdir/a_file.html
n18=; Query    => querystring=1
n19=; Fragment => anchor
n20=;
n21=;------------- $parse_url -------------
n22=
n23=
n24=alias parse_url {
n25=  if (?*tp://?*/* !iswm $1) return E_INVALID_STRING
n26=  var %a = $replace($1-,$chr(32),$chr(44)),%scheme = $gettok(%a,1,58),%h1 = $mid(%a,$calc($pos(%a,/,2) + 1),$calc($pos(%a,/,3) - $pos(%a,/,2) - 1)) 
n27=  if (*:*@* iswm %h1) var %host = $gettok(%h1,2-,64),%user = $left(%h1,$calc($pos(%h1,:) - 1)),%pass = $mid(%h1,$calc($pos(%h1,:) + 1),$calc($pos(%h1,@) - $pos(%h1,:) - 1))
n28=  else var %host = %h1
n29=  var %path = $gettok($right(%a,$+(-,$calc($pos(%a,/,3) - 1))),1,63),%query = $gettok($gettok($right(%a,$+(-,$calc($pos(%a,/,3) - 1))),2-,63),1,35),%fragment = $gettok($gettok($right(%a,$+(-,$calc($pos(%a,/,3) - 1))),2-,63),2-,35) 
n30=  return $iif(!$prop,%a,% [ $+ [ $prop ] ]) 
n31=}
